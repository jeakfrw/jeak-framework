import org.gradle.api.publish.maven.MavenPublication

publishing {
    repositories {
        mavenLocal()
        maven {
            def releaseRepository = 'https://nexus.fearnixx.de/repository/jeakbot-releases'
            def snapshotRepository = 'https://nexus.fearnixx.de/repository/jeakbot-snapshots'

            name = 'FNGNexus'
            url uri(project.version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository)

            if (project.hasProperty('fngNexusUser') && project.hasProperty('fngNexusPass')) {
                credentials {
                    username "${project.findProperty('fngNexusUser')}"
                    password "${project.findProperty('fngNexusPass')}"
                }
            }
        }
    }

    publications {
        api(MavenPublication) {
            artifactId = "${project.ext.artifact}-api"
            artifact apiJar
        }
        framework(MavenPublication) {
            artifactId = project.ext.artifact
            artifact jar
            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
}