group 'de.fearnixx'
def projectVersion = '1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (project.hasProperty("preRelease")) {
    apply from: './pre-release.gradle'
    def tag = project.ext.getPreReleaseTag()
    def i = tag.substring(tag.lastIndexOf('.') + 1)

    projectVersion += "-rc." + i


} else if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}

println "Using project version: $projectVersion"
version projectVersion

project.ext {
    artifact = 'jeakbot'
    
    confortVersion = '0.3.5-SNAPSHOT'
    log4jVersion = '2.11.1'
    hibernateVersion =  '5.3.6.Final'
    hikariCPVersion = '3.3.1'
    jakartaMailVersion = '1.6.3'

    connectors = [
        mysql: '8.0.13',
        mariadb: '2.1.2'
    ]
}

repositories {
    mavenLocal()
    maven {
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
    }

    main {
        java {
            srcDirs = ['src/api/java', 'src/main/java']
        }
        resources {
            srcDirs = ['src/api/resources', 'src/main/resources']
        }
    }
    
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

dependencies {
    apiCompile 'de.mlessmann:confort-api:0.3.5-SNAPSHOT'
    apiCompile 'org.hibernate:hibernate-core:5.3.6.Final'
    apiCompile 'org.slf4j:slf4j-api:1.7.25'

    compile sourceSets.api.output
    compile 'de.mlessmann:confort:0.3.5-SNAPSHOT'
    compile 'org.reflections:reflections:0.9.11'
    compile 'org.hibernate:hibernate-core:5.4.3.Final'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.zaxxer:HikariCP:3.3.1'
    compile 'com.sun.mail:mailapi:1.6.3'

    runtime 'org.antlr:antlr4-runtime:4.7.1'
    runtime 'org.apache.logging.log4j:log4j-api:2.11.1'
    runtime 'org.apache.logging.log4j:log4j-core:2.11.1'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.1'
    runtime 'mysql:mysql-connector-java:8.0.16'
    runtime 'org.mariadb.jdbc:mariadb-java-client:2.1.2'
    runtime 'com.sun.mail:smtp:1.6.3'
    runtime 'com.sun.mail:dsn:1.6.3'
    
    testCompile 'junit:junit:4.12'
}

apply from: './artifacts.gradle'
apply from: './distribution.gradle'
apply from: './publication.gradle'

build {
    dependsOn publishToMavenLocal

    if (!project.version.endsWith('SNAPSHOT')) {
        dependsOn tasks.zipDistribution
        dependsOn tasks.tarDistribution
    }
}

wrapper {
    gradleVersion '4.2.1'
}
