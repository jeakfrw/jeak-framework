group 'de.fearnixx'
version '1.0.3'
def projectVersion = project.version

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

if (project.hasProperty("preRelease")) {
    apply from: './pre-release.gradle'
    def tag = project.ext.getPreReleaseTag()
    def i = tag.substring(tag.lastIndexOf('.') + 1)

    projectVersion += "-rc." + i


} else if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}

println "Using project version: $projectVersion"
version projectVersion

project.ext {
    artifact = 'jeakbot'
}

repositories {
    mavenLocal()
    maven {
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
    }

    main {
        java {
            srcDirs = ['src/api/java', 'src/main/java']
        }
        resources {
            srcDirs = ['src/api/resources', 'src/main/resources']
        }
    }
    
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

dependencies {
    apiCompile 'de.mlessmann:confort-api:1.0.0-rc.1'
    apiCompile 'org.hibernate:hibernate-core:5.4.8.Final'
    apiCompile 'org.slf4j:slf4j-api:1.7.29'

    compile sourceSets.api.output

    compile 'de.mlessmann:confort:1.0.0-rc.1'
    compile 'org.reflections:reflections:0.9.10'
    compile 'org.hibernate:hibernate-core:5.4.8.Final'
    compile 'org.slf4j:slf4j-api:1.7.29'
    compile 'com.zaxxer:HikariCP:3.4.1'
    compile 'com.sun.mail:mailapi:1.6.4'
    compile 'com.sparkjava:spark-core:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.10.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.10.1'

    runtime 'org.antlr:antlr4-runtime:4.7.2'
    runtime 'org.apache.logging.log4j:log4j-api:2.12.1'
    runtime 'org.apache.logging.log4j:log4j-core:2.12.1'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.1'
    runtime 'mysql:mysql-connector-java:8.0.18'
    runtime 'org.mariadb.jdbc:mariadb-java-client:2.5.1'
    runtime 'com.sun.mail:smtp:1.6.4'
    runtime 'com.sun.mail:dsn:1.6.4'
    
    testCompile 'junit:junit:4.12'
}

apply from: './artifacts.gradle'
apply from: './distribution.gradle'
apply from: './publication.gradle'

compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}

build {
    dependsOn publishToMavenLocal
}

wrapper {
    gradleVersion '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}
