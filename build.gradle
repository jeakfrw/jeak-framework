group 'de.fearnixx'
def projectVersion = '1.0.2'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

if (project.hasProperty("preRelease")) {
    apply from: './pre-release.gradle'
    def tag = project.ext.getPreReleaseTag()
    def i = tag.substring(tag.lastIndexOf('.') + 1)

    projectVersion += "-rc." + i


} else if (!project.hasProperty('release')) {
    projectVersion += '-SNAPSHOT'
}

println "Using project version: $projectVersion"
version projectVersion

project.ext {
    artifact = 'jeakbot'
}

repositories {
    mavenLocal()
    maven {
        url uri('https://nexus.fearnixx.de/repository/maven-public')
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
    }

    main {
        java {
            srcDirs = ['src/api/java', 'src/main/java']
        }
        resources {
            srcDirs = ['src/api/resources', 'src/main/resources']
        }
    }
    
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

dependencies {
    apiCompile 'de.mlessmann:confort-api:1.0.0-rc.1'
    apiCompile 'org.hibernate:hibernate-core:5.4.4.Final'
    apiCompile 'org.slf4j:slf4j-api:1.7.27'

    compile sourceSets.api.output

    compile 'de.mlessmann:confort:1.0.0-rc.1'
    compile 'org.reflections:reflections:0.9.10'
    compile 'org.hibernate:hibernate-core:5.4.3.Final'
    compile 'org.slf4j:slf4j-api:1.7.27'
    compile 'com.zaxxer:HikariCP:3.3.1'
    compile 'com.sun.mail:mailapi:1.6.3'
    compile 'com.sparkjava:spark-core:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.10.0.pr3'

    runtime 'org.antlr:antlr4-runtime:4.7.2'
    runtime 'org.apache.logging.log4j:log4j-api:2.12.0'
    runtime 'org.apache.logging.log4j:log4j-core:2.12.0'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.0'
    runtime 'mysql:mysql-connector-java:8.0.17'
    runtime 'org.mariadb.jdbc:mariadb-java-client:2.4.3'
    runtime 'com.sun.mail:smtp:1.6.3'
    runtime 'com.sun.mail:dsn:1.6.4'
    
    testCompile 'junit:junit:4.12'
}

apply from: './artifacts.gradle'
apply from: './distribution.gradle'
apply from: './publication.gradle'

compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}

build {
    dependsOn publishToMavenLocal
}

wrapper {
    gradleVersion '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}
